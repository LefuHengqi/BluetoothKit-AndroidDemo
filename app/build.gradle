apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'  // 替换 kotlin-android-extensions

static def releaseTime() {
    def utcDate = new Date()
    def chinaTimeZone = TimeZone.getTimeZone("Asia/Shanghai") // 使用亚洲/上海时区代表中国时间
    def formattedDate = utcDate.format("yyyy-MM-dd_HH_mm", chinaTimeZone)
    return formattedDate
}

android {
    namespace 'com.lefu.ppblutoothkit'  // 添加 namespace 配置
    compileSdk 35  // 从34升级到35

    // 启用 BuildConfig 生成
    buildFeatures {
        buildConfig true
        viewBinding true
    }

    defaultConfig {
        applicationId "com.lefu.demo"
        minSdkVersion 24
        targetSdkVersion 35  // 添加这行，设置为35
        versionCode LIB_VERSION_CODE as int
        versionName LIB_VERSION
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude 'R.txt'
        exclude 'proguard.txt'
        exclude 'META-INF/**'
        exclude 'AndroidManifest.xml'

    }

    compileOptions {
        // 为排查 IDE “Cannot access Handler$Callback” 类型解析问题，临时下调到 Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        // 同步下调 Kotlin jvmTarget 到 11，避免 JDK17 + AAR 合并类引发的索引问题
        jvmTarget = '11'
    }
    /******************************以上Demo配置项，你的项目中无需引入*************************************/

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
    defaultConfig {
        multiDexEnabled true
    }

    bundle {
        density {
            // Different APKs are generated for devices with different screen densities; true by default.
            enableSplit true
        }
        abi {
            // Different APKs are generated for devices with different CPU architectures; true by default.
            enableSplit true
        }
        language {
            // This is disabled so that the App Bundle does NOT split the APK for each language.
            // We're gonna use the same APK for all languages.
            enableSplit true
        }
    }

    android.applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.outputs.all {
                outputFileName = "PPBluetoothKit_${variant.versionName}_${releaseTime()}.apk"
                assemble.doLast {
                    println "${outputFile.parent}"
                    print "$rootDir" + File.separator + "apk"
                    copy {
                        from outputFile.parent
                        include "*.apk"
                        into "$rootDir" + File.separator + "apk"
                    }
                    delete "${outputFile.parent}"
                }

            }
            if (variant.buildType.isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        include "*.txt"
                        into "$rootDir" + File.separator + "mapping" + File.separator + "${android.defaultConfig.versionName}"
                        rename {
                            "PPBluetoothKit_${variant.versionName}_mapping.txt"
                        }
                    }

                }
            }

        } else {
            variant.outputs.all {
                outputFileName = "PPBluetoothKit_${variant.versionName}_${releaseTime()}.apk"
            }
        }

    }

}

dependencies {
    // 排除本地 ppbluetoothkit AAR，避免与坐标依赖重复导致类冲突
    api fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.11.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'


    implementation project(':data_db')

//    implementation 'com.github.tbruyelle:rxpermissions:0.10.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'

    implementation files('libs\\iopp-1.0.3.jar')
    implementation files('libs\\okhttputils-2_6_2.jar')
    implementation 'com.squareup.okhttp3:okhttp:4.6.0'
    implementation 'com.alibaba:fastjson:1.2.76'

    // 权限请求框架：https://github.com/getActivity/XXPermissions
//    api 'com.github.getActivity:XXPermissions:20.0'
    api 'com.github.getActivity:XXPermissions:18.63'


//    api(project(":ppsdkkit:ppbasekit"))
//    api(project(":ppsdkkit:ppbluetoothkit"))
//    api(project(":ppsdkkit:ppcalculatekit"))
//    api(project(':ppbasiccalculatekit'))

    //基础组建库，必须集成
    api 'com.lefu.ppbasekit:ppbasekit:4.6.8'
    // 蓝牙库
    api 'com.lefu:bluetoothkit:1.5.5'
    //蓝牙数据协议解析库
    api 'com.lefu.ppbluetoothkit:ppbluetoothkit:4.6.8.4'
    //体脂数据解析库
    api 'com.lefu.ppcalculatekit:ppcalculatekit:4.6.8'
    //体脂计算库
    api 'com.lefu.ppbasiccalculatekit:ppbasiccalculatekit:1.1.0'
}

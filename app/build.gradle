apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.lefu.ppscalel.ble"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode LIB_VERSION_CODE as int
        versionName LIB_VERSION
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
    greendao {
        schemaVersion 1 //数据库版本
        targetGenDir 'src/main/java' //指定生成代码的目录
        daoPackage 'com.lefu.ppscale.ble.dao'//生成代码到具体包下
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PPScale_${variant.versionName}_${variant.name}_${releaseTime()}.apk"
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "com.android.support") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "26.+"
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'org.greenrobot:greendao:3.2.2'
//    implementation project(':ppscale')
    implementation project(':ppscale-new-master')
    implementation project(":body_sl")

    implementation 'com.android.support:multidex:1.0.3'

}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def releaseTime() {
//GMT+8代表北京时间
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

def fileName

def GROUP_ID = "com.lefu.ppbluetoothkit"
def ARTIFACT_ID = "ppbluetoothkit"

def BLUETOOTHKIT_GROUP_ID = "com.lefu"
def BLUETOOTHKIT_ARTIFACT_ID = "bluetoothkit"
def BLUETOOTHKIT_VERSION = "1.5.5"

android {
    namespace 'com.lefu.ppbluetoothkit'
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
    }


    defaultConfig {
        minSdkVersion 18
        versionCode LIB_VERSION_CODE as int
        versionName LIB_VERSION

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // 设置aar文件名包含版本号
        archivesBaseName = "${ARTIFACT_ID}-${LIB_VERSION}"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }



}

dependencies {
    // 仅打包 libs 下的 AAR，避免将本地 JAR（bluetoothkit-1.5.5.jar）合并进 AAR 导致重复类
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    // 作为编译依赖引入 bluetoothkit，真正的传递性由 POM 提供给下游工程
    api "${BLUETOOTHKIT_GROUP_ID}:${BLUETOOTHKIT_ARTIFACT_ID}:${BLUETOOTHKIT_VERSION}"

    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
//    api project(":ppsdkkit:lefulib:bluetoothkit")
    compileOnly(project(":ppsdkkit:ppbasekit"))

}


// 配置发布
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // 只发布aar文件，不包含.module和sources
                artifact("$buildDir/outputs/aar/${ARTIFACT_ID}-${LIB_VERSION}-release.aar")

                // 自定义你的包信息
                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version = LIB_VERSION

                // 在 POM 中声明 bluetoothkit 依赖（以及可能的传递依赖）
                pom.withXml {
                    def root = asNode()
                    def depsNode = root.appendNode('dependencies')

                    // bluetoothkit 主依赖
                    def btDep = depsNode.appendNode('dependency')
                    btDep.appendNode('groupId', BLUETOOTHKIT_GROUP_ID)
                    btDep.appendNode('artifactId', BLUETOOTHKIT_ARTIFACT_ID)
                    btDep.appendNode('version', BLUETOOTHKIT_VERSION)
                    btDep.appendNode('scope', 'compile')

                    // 如果 bluetoothkit 还依赖其他库，可在此追加（示例注释）：
                    // def another = depsNode.appendNode('dependency')
                    // another.appendNode('groupId', 'xxx')
                    // another.appendNode('artifactId', 'yyy')
                    // another.appendNode('version', 'zzz')
                    // another.appendNode('scope', 'compile')
                }
            }

            // 发布 bluetoothkit-1.5.5.jar 到同一仓库，便于 POM 解析
            bluetoothkitJar(MavenPublication) {
                groupId = BLUETOOTHKIT_GROUP_ID
                artifactId = BLUETOOTHKIT_ARTIFACT_ID
                version = BLUETOOTHKIT_VERSION
                artifact("$projectDir/libs/bluetoothkit-1.5.5.jar")
            }
        }

        repositories {
            maven {
                // 指定绝对路径到项目根目录下的maven文件夹
                url = uri("${rootProject.projectDir}/maven")
            }
        }
    }
    
    // 确保发布任务依赖于构建任务
    tasks.withType(PublishToMavenRepository) {
        dependsOn assembleRelease
    }
}


















